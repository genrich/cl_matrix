find_package(PackageHandleStandardArgs)

find_program(OCTAVE_CONFIG NAMES octave-config)

if(OCTAVE_CONFIG)
    execute_process(COMMAND ${OCTAVE_CONFIG} -p OCTINCLUDEDIR OUTPUT_VARIABLE OCTAVE_INCLUDE_PATHS   OUTPUT_STRIP_TRAILING_WHITESPACE)
    execute_process(COMMAND ${OCTAVE_CONFIG} -p OCTLIBDIR     OUTPUT_VARIABLE OCTAVE_LIBRARIES_PATHS OUTPUT_STRIP_TRAILING_WHITESPACE)
endif ()

find_library(OCTAVE_OCTINTERP_LIBRARY octinterp ${OCTAVE_LIBRARIES_PATHS})
find_library(OCTAVE_OCTAVE_LIBRARY    octave    ${OCTAVE_LIBRARIES_PATHS})
find_library(OCTAVE_CRUFT_LIBRARY     cruft     ${OCTAVE_LIBRARIES_PATHS})

find_path(OCTAVE_INCLUDE_DIR octave/oct.h ${OCTAVE_INCLUDE_PATHS} ${OCTAVE_INCLUDE_PATHS}/..)

find_package_handle_standard_args(Octave REQUIRED_VARS OCTAVE_OCTAVE_LIBRARY
                                                       OCTAVE_OCTINTERP_LIBRARY
                                                       OCTAVE_CRUFT_LIBRARY
                                                       OCTAVE_INCLUDE_DIR)

if(OCTAVE_FOUND)
    set(OCTAVE_LIBRARIES ${OCTAVE_OCTAVE_LIBRARY} ${OCTAVE_OCTINTERP_LIBRARY} ${OCTAVE_CRUFT_LIBRARY})
    set(OCTAVE_INCLUDE_DIRS ${OCTAVE_INCLUDE_DIR})
endif()

mark_as_advanced(OCTAVE_OCTINTERP_LIBRARY OCTAVE_OCTAVE_LIBRARY OCTAVE_CRUFT_LIBRARY OCTAVE_INCLUDE_DIR)
